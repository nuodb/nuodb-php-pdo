# (C) Copyright NuoDB, Inc. 2012-2018  All Rights Reserved.
# 
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
# 
#     * Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#     * Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in the
#       documentation and/or other materials provided with the distribution.
#     * Neither the name of NuoDB, Inc. nor the names of its contributors may
#       be used to endorse or promote products derived from this software
#       without specific prior written permission.
# 
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL NUODB, INC. BE LIABLE FOR ANY DIRECT,
# INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
# ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
# THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

#
# PHP PDO Native Database Driver for NuoDB
# 

PROJECT (NuoPhpPdo)
CMAKE_MINIMUM_REQUIRED (VERSION 2.8 FATAL_ERROR)

INCLUDE (CTest)
INCLUDE (InstallRequiredSystemLibraries)

SET (NUODB_PHPPDO_SOURCES
    nuodb_driver.c
    pdo_nuodb.c
    nuodb_statement.c
    php_pdo_nuodb_cpp_int.cpp
    php_pdo_nuodb_cpp_int.h
    php_pdo_nuodb_c_cpp_common.h
    php_pdo_nuodb_int.h
    php_pdo_nuodb.h
    config.h
    )

SET (CPACK_PACKAGE_NAME "NuoPhpPdo")
SET (CPACK_PACKAGE_VENDOR "NuoDB, Inc.")
SET (CPACK_PACKAGE_VERSION_MAJOR "1")
SET (CPACK_PACKAGE_VERSION_MINOR "0")
SET (CPACK_PACKAGE_VERSION_PATCH "1")
SET (CPACK_PACKAGE_VENDOR "NuoDB, Inc.")
SET (CPACK_PACKAGE_VERSION "${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}")
SET (CPACK_PACKAGE_DESCRIPTION_SUMMARY "A PHP PDO Native Database Driver for NuoDB")

SET (CPACK_GENERATOR "TGZ;ZIP")
SET (CPACK_DEBIAN_PACKAGE_MAINTAINER "NuoDB Support") #required

INCLUDE (CPack)

IF (APPLE)
   SET (CMAKE_MACOSX_RPATH ON)
ENDIF ()

FIND_PATH (PHP_INCLUDE_DIR main/php.h DOC "PHP includes directory")
IF (NOT PHP_INCLUDE_DIR)
    MESSAGE (FATAL_ERROR "PHP includes directory not found; please set CMAKE_INCLUDE_DIR variable")
ENDIF ()

FIND_PATH (NUODB_INCLUDE_DIR nuodb/NuoDB.h DOC "NuoDB includes directory")
IF (NOT NUODB_INCLUDE_DIR)
    MESSAGE (FATAL_ERROR "NuoDB includes directory not found; please set CMAKE_INCLUDE_DIR variable")
ENDIF ()

FIND_LIBRARY (NUOCLIENT_LIBRARY NAMES nuoclient
    DOC "NuoDB nuoclient library to link against")
IF (NOT NUOCLIENT_LIBRARY)
    MESSAGE (FATAL_ERROR "NuoDB nuoclient library not found; please set CMAKE_LIBRARY_PATH variable")
ENDIF ()

MESSAGE (STATUS "Build Type:               " ${CMAKE_BUILD_TYPE})
MESSAGE (STATUS "PHP includes directory:   " ${PHP_INCLUDE_DIR})
MESSAGE (STATUS "NuoDB includes directory: " ${NUODB_INCLUDE_DIR})
MESSAGE (STATUS "NuoDB nuoclient library:  " ${NUOCLIENT_LIBRARY})

GET_FILENAME_COMPONENT (NUODB_LIB_DIR ${NUOCLIENT_LIBRARY} DIRECTORY)

IF (CMAKE_GENERATOR MATCHES "Visual Studio 9 2008*")
  INCLUDE_DIRECTORIES (
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${PROJECT_SOURCE_DIR}/include_extras/vc9
    ${NUODB_INCLUDE_DIR}
    ${PHP_INCLUDE_DIR}
    ${PHP_INCLUDE_DIR}/main
    ${PHP_INCLUDE_DIR}/TSRM
    ${PHP_INCLUDE_DIR}/Zend
    ${PHP_INCLUDE_DIR}/ext
    ${PHP_INCLUDE_DIR}/ext/date/lib
    )
ELSE ()
  INCLUDE_DIRECTORIES (
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${NUODB_INCLUDE_DIR}
    ${PHP_INCLUDE_DIR}
    ${PHP_INCLUDE_DIR}/main
    ${PHP_INCLUDE_DIR}/TSRM
    ${PHP_INCLUDE_DIR}/Zend
    ${PHP_INCLUDE_DIR}/ext
    ${PHP_INCLUDE_DIR}/ext/date/lib
    )
ENDIF ()

ADD_LIBRARY (NuoPhpPdo SHARED ${NUODB_PHPPDO_SOURCES})
SET_TARGET_PROPERTIES (NuoPhpPdo PROPERTIES OUTPUT_NAME pdo_nuodb)

IF (NOT WIN32)
    ADD_DEFINITIONS (-DPHP_ATOM_INC)
    ADD_DEFINITIONS (-DHAVE_CONFIG_H)
    ADD_DEFINITIONS (-DHAVE_DLFCN_H=1)
    ADD_DEFINITIONS (-Wno-write-strings)
    ADD_DEFINITIONS (-Wall)
    IF (${USE_ZTS} MATCHES "1")
        MESSAGE("Using TS")
        ADD_DEFINITIONS (-DZTS=1)
    ENDIF ()
    LINK_DIRECTORIES (${NUODB_LIB_DIR})
    TARGET_LINK_LIBRARIES (NuoPhpPdo -L${NUODB_LIB_DIR} ${NUOCLIENT_LIBRARY})
    SET_TARGET_PROPERTIES (NuoPhpPdo PROPERTIES VERSION ${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH} SOVERSION ${CPACK_PACKAGE_VERSION_MAJOR})
    SET_TARGET_PROPERTIES (NuoPhpPdo PROPERTIES PREFIX "")
ENDIF ()

IF (CMAKE_GENERATOR MATCHES "Xcode")
    SET (CMAKE_SHARED_LINKER_FLAGS "-Wl,-flat_namespace -Wl,-undefined -Wl,suppress")
    SET_TARGET_PROPERTIES (NuoPhpPdo PROPERTIES SUFFIX .so)
ENDIF ()

IF (WIN32)
    FIND_LIBRARY (PHP_LIBRARY NAMES php5
        DOC "PHP php5 library to link against")
    IF (NOT PHP_LIBRARY)
        MESSAGE (FATAL_ERROR "PHP php5 library not found; please set CMAKE_LIBRARY_PATH variable")
    ENDIF ()
    MESSAGE (STATUS "PHP php5 library:  " ${PHP_LIBRARY})

    GET_FILENAME_COMPONENT (PHP_LIB_DIR ${PHP_LIBRARY} DIRECTORY)
    GET_FILENAME_COMPONENT (PHP_ROOT_DIR ${PHP_LIB_DIR} DIRECTORY)

    ADD_LIBRARY (php5 SHARED IMPORTED)

    IF (${USE_ZTS} MATCHES "1")
        SET_PROPERTY (TARGET php5 PROPERTY IMPORTED_LOCATION ${PHP_ROOT_DIR}/php5ts.dll)
    	SET_PROPERTY (TARGET php5 PROPERTY IMPORTED_IMPLIB ${PHP_LIB_DIR}/php5ts.lib)
    ELSE ()
    	SET_PROPERTY (TARGET php5 PROPERTY IMPORTED_LOCATION ${PHP_ROOT_DIR}/php5.dll)
    	SET_PROPERTY (TARGET php5 PROPERTY IMPORTED_IMPLIB ${PHP_LIB_DIR}/php5.lib)
    ENDIF ()

    IF (NOT NUODB_BIN_DIR)
        MESSAGE (FATAL_ERROR "NuoDB bin directory not found; please set NUODB_BIN_DIR variable")
    ENDIF ()

    MESSAGE (STATUS "NuoDB Bin Directory:      " ${NUODB_BIN_DIR})

    ADD_LIBRARY (nuoclient SHARED IMPORTED)
    SET_PROPERTY (TARGET nuoclient PROPERTY IMPORTED_LOCATION ${NUODB_BIN_DIR}/nuoclient.dll)
    SET_PROPERTY (TARGET nuoclient PROPERTY IMPORTED_IMPLIB ${NUOCLIENT_LIBRARY})

    IF (${USE_ZTS} MATCHES "1")
	SET_PROPERTY (TARGET NuoPhpPdo PROPERTY COMPILE_DEFINITIONS ZTS=1 HAVE_CONFIG_H ZEND_DEBUG=0 COMPILE_DL_NUODB PHP_WIN32 ZEND_WIN32)
	SET_PROPERTY (TARGET NuoPhpPdo PROPERTY COMPILE_DEFINITIONS_$<$<CONFIG:Debug>:DEBUG_MODE> ZTS=1 HAVE_CONFIG_H ZEND_DEBUG=1 COMPILE_DL_NUODB PHP_WIN32 ZEND_WIN32)
    ELSE ()
	SET_PROPERTY (TARGET NuoPhpPdo PROPERTY COMPILE_DEFINITIONS HAVE_CONFIG_H ZEND_DEBUG=0 COMPILE_DL_NUODB PHP_WIN32 ZEND_WIN32)
	SET_PROPERTY (TARGET NuoPhpPdo PROPERTY COMPILE_DEFINITIONS_$<$<CONFIG:Debug>:DEBUG_MODE> HAVE_CONFIG_H ZEND_DEBUG=1 COMPILE_DL_NUODB PHP_WIN32 ZEND_WIN32)
    ENDIF ()

    TARGET_LINK_LIBRARIES (NuoPhpPdo nuoclient php5)
ENDIF ()

INSTALL (TARGETS NuoPhpPdo
    ARCHIVE DESTINATION lib
    LIBRARY DESTINATION lib
    )

ADD_TEST (001.phpt tests/001.phpt)
